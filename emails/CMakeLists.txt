set(EMAIL_TEMPLATE_INPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/src/emails")
set(EMAIL_TEMPLATE_HTML_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/emails/output/html")
set(EMAIL_TEMPLATE_TXT_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/emails/output/html")
file(MAKE_DIRECTORY ${EMAIL_TEMPLATE_HTML_OUTPUT_DIRECTORY})
file(MAKE_DIRECTORY ${EMAIL_TEMPLATE_TXT_OUTPUT_DIRECTORY})

set(EMAIL_IN_TREE_OUTPUT "${CMAKE_SOURCE_DIR}/server/communication/email_templates")

# WARNING: This does not exclude any spec or story files.
file(GLOB_RECURSE EMAIL_TEMPLATE_INPUTS "${EMAIL_TEMPLATE_INPUT_DIRECTORY}/*/index.tsx")

set(TEMPLATE_OUTPUTS)
foreach(FILE ${EMAIL_TEMPLATE_INPUTS})
  string(REPLACE "${EMAIL_TEMPLATE_INPUT_DIRECTORY}/" "" TEMPLATE_NAME "${FILE}")
  string(REPLACE "/index.tsx" "" TEMPLATE_NAME "${TEMPLATE_NAME}")
  set(HTML_TEMPLATE_DIR "${EMAIL_TEMPLATE_HTML_OUTPUT_DIRECTORY}/html/${TEMPLATE_NAME}")
  set(HTML_TEMPLATE "${HTML_TEMPLATE_DIR}/index.html")
  set(HTML_IN_TREE_TEMPLATE "${EMAIL_IN_TREE_OUTPUT}/${TEMPLATE_NAME}.html")
  set(TXT_TEMPLATE_DIR "${EMAIL_TEMPLATE_TXT_OUTPUT_DIRECTORY}/txt/${TEMPLATE_NAME}")
  set(TXT_TEMPLATE "${TXT_TEMPLATE_DIR}/index.txt")
  set(TXT_IN_TREE_TEMPLATE "${EMAIL_IN_TREE_OUTPUT}/${TEMPLATE_NAME}.txt")
  add_custom_command(
    OUTPUT ${HTML_IN_TREE_TEMPLATE}
    BYPRODUCTS ${HTML_IN_TREE_TEMPLATE}
    COMMAND ${REACT_EMAIL_EXECUTABLE} export --silent --dir src/emails/${TEMPLATE_NAME} --outDir ${HTML_TEMPLATE_DIR}
    COMMAND ${CMAKE_COMMAND} -E rename ${HTML_TEMPLATE} ${HTML_IN_TREE_TEMPLATE}
    COMMENT "Building ${TEMPLATE_NAME} HTML email templates"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DEPENDS
      tools.react-email
      ${FILE}
  )

  add_custom_command(
    OUTPUT ${TXT_IN_TREE_TEMPLATE}
    BYPRODUCTS ${TXT_IN_TREE_TEMPLATE}
    COMMAND ${REACT_EMAIL_EXECUTABLE} export --plainText --silent --dir src/emails/${TEMPLATE_NAME} --outDir ${TXT_TEMPLATE_DIR}
    COMMAND ${CMAKE_COMMAND} -E rename ${TXT_TEMPLATE} ${TXT_IN_TREE_TEMPLATE}
    COMMENT "Building ${TEMPLATE_NAME} TXT email templates"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DEPENDS
      tools.react-email
      ${FILE}
  )

  list(APPEND TEMPLATE_OUTPUTS "${HTML_IN_TREE_TEMPLATE}")
  list(APPEND TEMPLATE_OUTPUTS "${TXT_IN_TREE_TEMPLATE}")
endforeach()

add_custom_target(
  build.email
  COMMAND ${CMAKE_COMMAND} -E true # no-op
  DEPENDS ${TEMPLATE_OUTPUTS}
)

add_custom_target(
  development.email
  COMMENT "Running the local development tools for email templates"
  COMMAND ${REACT_EMAIL_EXECUTABLE} dev --dir src/emails
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  DEPENDS
    tools.react-email
)
